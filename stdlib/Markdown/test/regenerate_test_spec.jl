# Parse the CommonMark spec tests in spec.json (which come from
# https://spec.commonmark.org/0.31.2/spec.json) and check the Markdown
# stdlib against it. Also regenerate the test_spec.jl file from
# this, to be used in regression tests.
#
# There are two main reasons to run this script:
# 1. to adjust the tests after a change to the Markdown test suite
# 2. after updating spec.json
#

using Markdown
using JSON
using Test
using StyledStrings

function check_commonmark_spec_html1(tst; report::Bool=false)
    input = tst["markdown"]
    parsed = Markdown.parse(input)
    expected = tst["html"]
    actual = Markdown.html(parsed)
    if expected != actual && report
        printstyled("============================================\n"; color=:cyan)
        println("failed test ", tst["example"], ":")
        print("input:\n'"); printstyled(input; color=:bold); println("'")
        print("parsed:\n'"); printstyled(parsed; color=:yellow); println("'")
        print("expected HTML:\n'"); printstyled(expected; color=:green); println("'")
        print("actual HTML:\n'"); printstyled(actual; color=:red); println("'")
        println()
    end
    return expected == actual
end

function check_commonmark_spec_html(data; report::Bool=false)
    failed = Int[]
    for tst in data
        check_commonmark_spec_html1(tst; report) || push!(failed, tst["example"])
    end
    if report
        total = length(data)
        passed = total - length(failed)
        println("HTML score: $passed/$total = $(passed/total*100)% pass")
    end
    return BitSet(failed)
end

function escape_spec_string(s::String)
    s = Base.escape_string(s)
    # avoid string interpolation
    s = replace(s, "\$" => raw"\$")
    # encode non-breaking whitespace to make contrib/check-whitespace.jl happy
    s = replace(s, "\uA0" => raw"\uA0")
    return s
end

function generate_test_file(io::IO, data, broken)
    println(io, """
        # WARNING: this file was generated by $(basename(@__FILE__)), do not edit it directly.

        using Test
        using Markdown

        @testset verbose=true "CommonMark spec test suite" begin
        """)

    section = ""
    for tst in data
        if section != tst["section"]
            section != "" && println(io, "end")  # end testset
            section = tst["section"]
            println(io)
            println(io, """
                #
                # $section
                #
                @testset "$section" begin
                """)
        end

        println(io, "    # Example ", tst["example"])
        println(io, "    input = \"", escape_spec_string(tst["markdown"]), "\"")
        println(io, "    expected = \"", escape_spec_string(tst["html"]), "\"")
        println(io, "    actual = Markdown.html(Markdown.parse(input))");
        if tst["example"] in broken
            println(io, "    @test_broken expected == actual");
        else
            println(io, "    @test expected == actual");
        end
        println(io)
    end
    section != "" && println(io, "end")  # end testset
    println(io)
    println(io, "end")
end

# parse the spec
data = JSON.parsefile(joinpath(@__DIR__, "spec.json"))

# check all spec tests and record which are broken
broken = check_commonmark_spec_html(data)

# generate output file
open(joinpath(@__DIR__, "test_spec.jl"), "w") do io
    generate_test_file(io, data, broken)
end
